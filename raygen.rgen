#version 460
#extension GL_EXT_ray_tracing : enable

layout(binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, rgba8) uniform image2D image;

layout(binding = 2) uniform CameraProperties {
    mat4 projInverse;
    mat4 viewInverse;
} cam;

layout(location = 0) rayPayloadEXT vec3 hitValue;

void main()
{
    vec2 pos = gl_LaunchIDEXT.xy;
    const vec2 pixelCenter = vec2(pos) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec4 origin = cam.viewInverse * vec4(0.0, 0.0, 0.0, 1.0);
    vec4 target = cam.projInverse * vec4(d.x, d.y, 1.0, 1.0);
    vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0.0);

    float tmin = 0.001;
    float tmax = 10000.0;

    hitValue = vec3(1.0, 0.0, 0.0); // dummy, debug

    traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);

    imageStore(image, ivec2(pos), vec4(hitValue, 1.0));
}
